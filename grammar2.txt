S               : <RE>
                ;
<RE>	        : <union> | <simple-RE>
                ;
<union>	        :	<RE> "|" <simple-RE>
                ;
<simple-RE>	    : <concatenation> | <basic-RE>
                ;
<concatenation>	:	<simple-RE> <basic-RE>
                ;
<basic-RE>	    :	<star> | <plus> | <elementary-RE>
                ;
<star>	        :	<elementary-RE> "*"
                ;
<plus>	        :	<elementary-RE> "+"
                ;
<elementary-RE>	:	<group> | <any> | <eos> | <char> | <set>
                ;
<group>	        :	"(" <RE> ")"
                ;
<any>	        :	"."
                ;
<eos>	        :	"$"
                ;
<char>	        :	"a" | "b" | "1" | "2"
                ;
<set>	        :	<positive-set> | <negative-set>
                ;
<positive-set>	:	"[" <set-items> "]"
                ;
<negative-set>	:	"[" "^" <set-items> "]"
                ;
<set-items>	    :	<set-item> | <set-item> <set-items>
                ;
<set-item>	    :	<range> | <char>
                ;
<range>	        :	<char> "-" <char>
                ;